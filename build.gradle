import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated/src/main/java"
			srcDir "${buildDir}/generated/client/src/main/java"
		}
	}
}

tasks.named('bootJar') {
	mainClass = 'bg.coincraft.userservice.UserServiceApplication'
}

bootJar {
	enabled = true
	mainClass = 'bg.coincraft.userservice.UserServiceApplication'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// postgresql
	runtimeOnly 'org.postgresql:postgresql'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// ============================================
// SERVER CODE: Generate APIs for this service
// ============================================
buildServerCode {
	inputSpec = "$projectDir/../../openapi/user-service/user-service.yml".toString()
	outputDir = "$buildDir/generated".toString()
	apiPackage = "bg.coincraft.userservice.web"
	modelPackage = "bg.coincraft.userservice.model"
}

tasks.register('buildAuthenticationService', GenerateTask) {
	generatorName = "java"

	inputSpec = "$projectDir/../../openapi/authentication-service/authentication-service.yml".toString()
    outputDir = "${buildDir}/generated/client".toString()
	modelPackage = "bg.coincraft.authenticationservice.client.model"

	enablePostProcessFile = true
    skipOverwrite = false
    modelNameSuffix = "DTO"

	configOptions = [
			delegatePattern               : "true",
			async                         : "false",
			useOptional                   : "true",
			java8                         : "true",
			dateLibrary                   : "java8",
			serializationLibrary          : "jackson",
			library                       : "resttemplate",
			useBeanValidation             : "true",
			interfaceOnly                 : "false",
			serializableModel             : "true",
			withXml                       : "false",
			useTags                       : "true",
			unhandledException            : "true",
			additionalModelTypeAnnotations: "@lombok.Builder(setterPrefix = \"set\") @lombok.AllArgsConstructor",
			useJakartaEe                  : "true",
			useSpringBoot3                : "true"
	]
}

compileJava {
	dependsOn(tasks.buildServerCode, tasks.buildAuthenticationService)
}